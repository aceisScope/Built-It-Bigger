apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(path: ':libjoker')
    compile project(path: ':libjokedisplay')
    compile 'com.android.support:appcompat-v7:25.0.0'
    freeCompile 'com.google.android.gms:play-services-ads:9.8.0'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile project(path: ':backend', configuration: 'android-endpoints')
}

task runAppEngine (dependsOn: ":backend:appengineRun") {
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
    doLast {
        println "Server Started"
    }
}

task runAndroidTest (dependsOn: ":app:connectedCheck") {
    shouldRunAfter 'runAppEngine'
    doLast {
        println "AndroidTest Finished"
    }
}

task stopAppEngine (dependsOn: ":backend:appengineStop") {
    shouldRunAfter 'runAndroidTest'
    doLast {
        println "Server Stopped"
    }
}

task runAndroidTestWithServer {
    dependsOn = ['runAppEngine', 'runAndroidTest', 'stopAppEngine']
}